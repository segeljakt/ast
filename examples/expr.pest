WHITESPACE   =  _{ " " | "\t" | NEWLINE }
ident        =  @{ ( ASCII_ALPHA | "_" ) ~ ( ASCII_ALPHANUMERIC | "_" )* }

program      =   { SOI ~ expr ~ EOI }
  expr       =   { binop }
    binop    =  _{ unop ~ ( infix ~ unop )* }
      infix  =  _{ add | mul }
        add  =   { "+" }
        mul  =   { "*" }
    unop     =   { prefix* ~ primary ~ suffix* }
      prefix =  _{ not | neg }
        not  =   { "!" }
        neg  =   { "-" }
      suffix =  _{ try | call }
        try  =   { "?" }
        call =   { "." ~ ident ~ "(" ~ expr ~ ( "," ~ expr )* ~ ")" }
    primary  =  _{ term | "(" ~ expr ~ ")" }
      term   =   { ident }
