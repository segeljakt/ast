WHITESPACE  =  _{ " " | "\t" | NEWLINE }
ident       =  @{ ( ASCII_ALPHA | "_" ) ~ ( ASCII_ALPHANUMERIC | "_" )* }

program     =   { SOI ~ expr ~ EOI }
  expr      =   { prefix* ~ primary ~ suffix* ~ ( infix ~ prefix* ~ primary ~ suffix* )* }
    infix   =  _{ add | sub | mul | div }
      add   =   { "+" }
      sub   =   { "-" }
      mul   =   { "*" }
      div   =   { "/" }
    prefix  =  _{ not | neg }
      not   =   { "!" }
      neg   =   { "-" }
    suffix  =  _{ try | call }
      try   =   { "?" }
      call  =   { "." ~ ident ~ "(" ~ expr ~ ( "," ~ expr )* ~ ")" }
    primary =  _{ term | "(" ~ expr ~ ")" }
      term  =   { ident }
